//CIFCEISF JOB MSGCLASS=X,MSGLEVEL=(1,1),REGION=64M,NOTIFY=&SYSUID      JOB29022
//*-------- ------------------------------------------------------------
//* ESMR 2018-1152 PERKESO EIS FCLB CONTRIBUTION
//* ABEND 77 : INCOMPLETE FILE
//* ABEND 88 : TOTAL DETAIL RECORD NOT TALLY WITH TOTAL AT FOOTER
//*
//* NOTICEID(17) = EMPLOYERID(12)+YEAR(2)+RUNNING NUMBER(3)
//* DROP RECORDS IF NOTICEID NOT 17 DIGITS(NO RUNNING NUMBER)
//*
//* SORT UNIQUE RECORDS ONLY
//* SAME EMPLOYERID,NOTICE ID WITH DIFFERENT AMT - REJECT DURING LOADING
//*---------------------------------------------------------------------
//DELETE   EXEC PGM=IEFBR14
//DEL1     DD DSN=RBP2.B033.PERKESO.FCLBEISC.FULLLOAD,
//            DISP=(MOD,DELETE,DELETE),SPACE=(TRK,(0))
//*---------------------------------------------------------------------
//FCLBFULL EXEC SAS609
//FCLBEISC DD DISP=SHR,DSN=RBP2.B033.PERKESO.FCLBEISC.FULL(0)
//OUTFILE  DD DSN=RBP2.B033.PERKESO.FCLBEISC.FULLLOAD,
//            DISP=(NEW,CATLG,DELETE),
//            UNIT=SYSDA,SPACE=(CYL,(300,300),RLSE),
//            DCB=(LRECL=143,BLKSIZE=0,RECFM=FB)
//SASLIST  DD SYSOUT=X
//SYSIN    DD *
OPTION NOCENTER;
 DATA FCLBFULL;
    RETAIN X;
    INFILE FCLBEISC END=LAST;
      INPUT @01    RECORD_TYPE     $1.        /* VALUE H , D , F */
            @02    NEW_EMPL_CODE   $12.
            @02    TOTAL_REC       $7.        /* FOR FOOTER TOTAL*/
            @14    EMPL_NAME       $100.      /* TRUNCATE TO 40 */
            @114   NOTICEID        $17.
            @131   AMOUNT          $14.;
      IF RECORD_TYPE = 'H' THEN DELETE;
      IF RECORD_TYPE = 'D' THEN X+1;
      IF RECORD_TYPE = 'F' THEN DO;
         TOTAL_REC_NUM = TOTAL_REC * 1;
         IF TOTAL_REC_NUM NE X THEN ABORT 88;
      END;

      /* ----------------------------------------- */
      /* INVALID NOTICE ID .NO RUNNING NUMBER      */
      /* ----------------------------------------- */
      IF SUBSTR(NOTICEID,15,3) = '   ' THEN DELETE;

      /* ------------------------- */
      /* CHECK FOR INCOMPLETE FILE */
      /* ------------------------- */
      IF RECORD_TYPE = 'F' THEN F+1;
      IF LAST AND F = 0 THEN ABORT 77;

 RUN;
 PROC PRINT DATA=FCLBFULL(OBS=100);TITLE 'FCLB FILE';RUN;
 PROC SORT  DATA=FCLBFULL NODUPKEY ;BY NEW_EMPL_CODE
                                       EMPL_NAME
                                       NOTICEID
                                       AMOUNT; RUN;
 PROC SORT  DATA=FCLBFULL;BY NEW_EMPL_CODE NOTICEID EMPL_NAME AMOUNT;
 RUN;

 DATA _NULL_;
   SET FCLBFULL;BY NEW_EMPL_CODE NOTICEID EMPL_NAME AMOUNT;
   FILE OUTFILE;
      /* POSITION FOR NAME AND NOTICEID IS SWITCHED AS   */
      /* IS BEING USED AS PRIMARY KEY FOR CIFCEIST TABLE */
      IF RECORD_TYPE = 'F' THEN DELETE;
      PUT @001  NEW_EMPL_CODE   $12.
          @013  NOTICEID        $17.
          @030  EMPL_NAME       $100.
          @130  AMOUNT          $14.;
   RETURN;
   RUN;
