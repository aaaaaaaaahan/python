//CICUSDPR JOB MSGCLASS=X,MSGLEVEL=(1,1),REGION=8M,NOTIFY=&SYSUID       JOB19340
//*--------------------------------------------------------------------
//*ESMR 2018 - 3419
//*EXTRACT ALL CIS CUSTOMERS WITH FOLLOWING CRITERIA:
//*(1)INDIVIDUAL CUST WITHOUT ALIAS TYPE 'IC'
//*(2)HOLDING 'MY' CITIZENSHIP
//*(3)RELATED ALL DEPOSIT ACCOUNTS
//*--------------------------------------------------------------------
//*ESMR 2020-541 INCLUDE EXTRACTION CUSTOMER WITHOUT ANY IDS
//*--------------------------------------------------------------------
//* INITIALIZE DATASETS
//*--------------------------------------------------------------------
//INITDASD EXEC PGM=IEFBR14
//DEL1     DD DSN=RBP2.B033.CIS.RELDP.CUSTS,
//            DISP=(MOD,DELETE,DELETE),UNIT=SYSDA,SPACE=(TRK,(0))
//DEL2     DD DSN=RBP2.B033.CIS.RELDP.CUSNOID.WTHACCT,
//            DISP=(MOD,DELETE,DELETE),UNIT=SYSDA,SPACE=(TRK,(0))
//*--------------------------------------------------------------------
//* PROCESSING STARTS HERE
//*--------------------------------------------------------------------
//MERGE#01 EXEC SAS609
//RLEN#CA  DD DISP=SHR,DSN=RBP2.B033.UNLOAD.RLEN#CA
//CUSTFILE DD DISP=SHR,DSN=RBP2.B033.UNLOAD.ALLCUST.FB
//IDFILE   DD DISP=SHR,DSN=RBP2.B033.UNLOAD.ALLALIAS.FB
//OUTFILE  DD DSN=RBP2.B033.CIS.RELDP.CUSTS,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=100,BLKSIZE=0),
//            SPACE=(CYL,(100,100),RLSE),UNIT=SYSDA
//OUTFILE2 DD DSN=RBP2.B033.CIS.RELDP.CUSNOID.WTHACCT,
//            DISP=(NEW,CATLG,DELETE),
//            DCB=(RECFM=FB,LRECL=100,BLKSIZE=0),
//            SPACE=(CYL,(100,100),RLSE),UNIT=SYSDA
//SASLIST  DD SYSOUT=X
//SYSIN    DD *
OPTIONS NOCENTER;
TITLE;
DATA RLEN;
  INFILE RLEN#CA;
  INPUT  @003  BANKNO            PD2.
         @005  ACCTNOC           $20.
         @005  ACCTNO            20.
         @025  ACCTCODE          $5.
         @046  CUSTNO            $11.
         @066  RLENCODE          PD2.
         @068  PRISEC            PD2.;
       IF ACCTCODE = 'DP   ' THEN OUTPUT;
PROC SORT  DATA=RLEN; BY CUSTNO ; RUN;

DATA CUST;
  INFILE CUSTFILE;
  INPUT  @005  CUSTNO           $11.
         @033  GENDER            $1.
         @034  CUSTSTAT          $1.
         @035  TAXCODE           $1.
         @036  TAXID             $9.
         @045  CUSTBRCH         PD4.
         @049  COSTCTR          PD4.
         @053  CUSTLASTDATECC   $2.
         @055  CUSTLASTDATEYY   $2.
         @058  CUSTLASTDATEMM   $2.
         @061  CUSTLASTDATEDD   $2.
         @063  CUSTLASTOPER     $8.
         @108  RACE             $1.
         @109  CITIZENSHIP      $2.
         @119  CUSTOPENDATE     PD6.
         @232  CUSTCONSENT      $3.;
         IF GENDER='O' THEN INDORG='O';
         ELSE INDORG = 'I';
         IF CITIZENSHIP EQ 'MY' AND INDORG = 'I' THEN OUTPUT;
PROC SORT  DATA=CUST; BY CUSTNO ;RUN;

DATA ICID;
  INFILE IDFILE;
  INPUT  @005  CUSTNO             $11.
         @089  ALIASKEY           $3.
         @092  ALIAS              $20.;
PROC SORT  DATA=ICID NODUPKEY; BY CUSTNO ;RUN;
*;
 PROC SQL;
     CREATE TABLE MERGE1 AS
     SELECT RLEN.BANKNO
           ,RLEN.ACCTNOC
           ,RLEN.ACCTNO
           ,RLEN.ACCTCODE
           ,RLEN.RLENCODE
           ,RLEN.PRISEC
           ,ICID.CUSTNO
           ,ICID.ALIASKEY
           ,ICID.ALIAS
     FROM RLEN,ICID
     WHERE RLEN.CUSTNO = ICID.CUSTNO
     AND   ICID.ALIASKEY NOT = 'IC';
 QUIT;

 PROC SORT  DATA=MERGE1; BY CUSTNO; RUN;
 PROC PRINT DATA=MERGE1(OBS=10);TITLE 'CUST WITH NO IC';RUN;

DATA ALLDP;
   MERGE CUST(IN=A) MERGE1(IN=B); BY CUSTNO;
   IF A AND B THEN DO;
      OUTPUT ALLDP;
   END;
RUN;
PROC SORT  DATA=ALLDP; BY CUSTNO;RUN;
PROC PRINT DATA=ALLDP(OBS=10);TITLE 'ALL DP CUSTOMERS';RUN;

 /*---------------------------------------------------------*/
 /* PROCESSING FOR CUSTOMER WITHOUT ANY IDS  *SMR 2020-541  */
 /*---------------------------------------------------------*/
DATA NOID;
   MERGE CUST(IN=C) ICID(IN=D); BY CUSTNO;
   IF C AND NOT D THEN DO;
      OUTPUT NOID;
   END;
RUN;
PROC SORT  DATA=NOID; BY CUSTNO;RUN;
PROC PRINT DATA=NOID(OBS=10);TITLE 'CUST NO ID';RUN;

DATA NOIDREL;
   MERGE NOID(IN=E) RLEN(IN=F); BY CUSTNO;
   IF E AND F THEN DO;
      OUTPUT NOIDREL;
   END;
RUN;
PROC SORT  DATA=NOIDREL; BY CUSTNO;RUN;
PROC PRINT DATA=NOIDREL(OBS=10);TITLE 'CUST NO ID WITH REL';RUN;

 /*----------------------------------------------------------------*/
 /*   OUTPUT CUST WITH DP ACCTS WITH NO IDS                         */
 /*----------------------------------------------------------------*/
DATA OUTNOID;
SET NOIDREL;
FILE OUTFILE2;
     PUT @01   CUSTBRCH            Z7.
         @08   CUSTNO              $11.
         @19   ACCTCODE            $5.
         @24   ACCTNO              20.
         @44   RACE                $1.
         @45   ALIASKEY            $3.
         @48   ALIAS               $20.
         @68   CITIZENSHIP         $2.;
  RUN;

 /*----------------------------------------------------------------*/
 /*  OUTPUT CUST WITH OTHER IDS AND WITH NO IDS INCLUDE DP ACCTS   */
 /*----------------------------------------------------------------*/
DATA TEMPOUT;
SET ALLDP NOIDREL;
FILE OUTFILE;
     PUT @01   CUSTBRCH            Z7.
         @08   CUSTNO              $11.
         @19   ACCTCODE            $5.
         @24   ACCTNO              20.
         @44   RACE                $1.
         @45   ALIASKEY            $3.
         @48   ALIAS               $20.
         @68   CITIZENSHIP         $2.;
  RUN;
