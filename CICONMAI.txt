//CICONMAI JOB MSGCLASS=X,MSGLEVEL=(1,1),REGION=64M,NOTIFY=&SYSUID      JOB68724
//*--------------------------------------------------------------------
//*-2013-2007 DAILY UPDATE FOR CUNEG
//*-       GENERATE 3 FILES
//*        UPDATE CICUSTT  - CUSTOMER TABLE(CIUPDMS7)
//*        UPDATE CICON1ST - CUSTOMER CONSENT TABLE(CIUPDCON)
//*        UPDATE CIRMRKT  - AUTO DONE BY PROGRAM CICONSRM(BATCHDATE)
//* ESMR 2013-2007 UPDATING CONSENT FROM UNICARD TO CIS (DROPPED)
//* ESMR 2014-827  UPDATING CONSENT FROM UNICARD TO CIS (NEW)
//*--------------------------------------------------------------------
//INITDASD EXEC PGM=IEFBR14
//DEL1     DD DSN=RBP2.B033.UNICARD.MAILFLAG.LIST,
//            DISP=(MOD,DELETE,DELETE),UNIT=SYSDA,SPACE=(TRK,(0))
//DEL2     DD DSN=RBP2.B033.UNICARD.MAILFLAG.UPDCUST,
//            DISP=(MOD,DELETE,DELETE),UNIT=SYSDA,SPACE=(TRK,(0))
//DEL3     DD DSN=RBP2.B033.UNICARD.MAILFLAG.UPDCON1,
//            DISP=(MOD,DELETE,DELETE),UNIT=SYSDA,SPACE=(TRK,(0))
//DEL4     DD DSN=RBP2.B033.UNICARD.MAILFLAG.UNQ,
//            DISP=(MOD,DELETE,DELETE),UNIT=SYSDA,SPACE=(TRK,(0))
//*--------------------------------------------------------------------
//*- SORT UNIQUE BY IC AND ID
//*--------------------------------------------------------------------
//UNQCUST  EXEC PGM=ICETOOL
//TOOLMSG  DD SYSOUT=*
//DFSMSG   DD SYSOUT=*
//INDD01   DD DISP=SHR,DSN=RBP2.B033.UNICARD.MAILFLAG
//OUTDD01  DD DSN=RBP2.B033.UNICARD.MAILFLAG.UNQ,
//            DISP=(NEW,CATLG,DELETE),
//            SPACE=(CYL,(100,100),RLSE),UNIT=SYSDA,
//            DCB=(LRECL=50,BLKSIZE=0,RECFM=FB)
//TOOLIN   DD *
   SELECT FROM(INDD01) TO(OUTDD01) ON(1,27,CH) FIRST
//*--------------------------------------------------------------------
//STATS#01 EXEC SAS609,OPTIONS='VERBOSE SORTLIST SORTMSG MSGLEVEL=I'
//MAILFLAG DD DISP=SHR,DSN=RBP2.B033.UNICARD.MAILFLAG.UNQ
//CTRLDATE DD DISP=SHR,DSN=RBP2.B033.SRSCTRL1(0)
//ALSFILE  DD DISP=SHR,DSN=RBP2.B033.UNLOAD.ALLALIAS.FB
//CUSTFILE DD DISP=SHR,DSN=RBP2.B033.CIS.CUST.DAILY
//OUTFILE  DD DSN=RBP2.B033.UNICARD.MAILFLAG.LIST,
//            DISP=(NEW,CATLG,DELETE),
//            SPACE=(CYL,(100,100),RLSE),UNIT=SYSDA,
//            DCB=(LRECL=200,BLKSIZE=0,RECFM=FB)
//UPDCUST  DD DSN=RBP2.B033.UNICARD.MAILFLAG.UPDCUST,
//            DISP=(NEW,CATLG,DELETE),
//            SPACE=(CYL,(100,100),RLSE),UNIT=SYSDA,
//            DCB=(LRECL=200,BLKSIZE=0,RECFM=FB)
//UPDCON1  DD DSN=RBP2.B033.UNICARD.MAILFLAG.UPDCON1,
//            DISP=(NEW,CATLG,DELETE),
//            SPACE=(CYL,(100,100),RLSE),UNIT=SYSDA,
//            DCB=(LRECL=120,BLKSIZE=0,RECFM=FB)
//EXCEPT   DD DSN=RBP2.B033.UNICARD.MAILFLAG.RPT(+1),
//            DISP=(NEW,CATLG,DELETE),
//            SPACE=(CYL,(100,100),RLSE),UNIT=SYSDA,
//            DCB=(LRECL=134,BLKSIZE=0,RECFM=FBA)
//SASLIST  DD SYSOUT=X
//SYSIN    DD *
OPTIONS NOSORTBLKMODE;
 /*----------------------------------------------------------------*/
 /*    SET DATES                                                   */
 /*----------------------------------------------------------------*/
    DATA SRSDATE;
          INFILE CTRLDATE;
            INPUT @001  SRSYY    4.
                  @005  SRSMM    2.
                  @007  SRSDD    2.;

          /* DISPLAY TODAY REPORTING DATE*/
          TODAYSAS=MDY(SRSMM,SRSDD,SRSYY);
          CALL SYMPUT('DATE1',PUT(TODAYSAS,YYMMDDD10.));
          CALL SYMPUT('DATE2',PUT(TODAYSAS,DATE9.));
    RUN;
   PROC PRINT;RUN;
 /*----------------------------------------------------------------*/
 /*    PROCESSING                                                  */
 /*----------------------------------------------------------------*/
DATA CIS;
   FORMAT OLDIC $12. CISOLDIC  $12.;
   SET CUSTFILE.CUSTDLY;
   KEEP CUSTNO CISCONSENT TAXID OLDIC INDORG CISOLDIC;
   OLDIC = TAXID;
   CISOLDIC = TAXID;
   IF CUSTCONSENT = '001' THEN CISCONSENT = 'Y';
   ELSE IF CUSTCONSENT = '002' THEN CISCONSENT = 'N';
   ELSE CISCONSENT = ' ';
RUN;
PROC SORT  DATA=CIS NODUPKEY; BY CUSTNO;RUN;

DATA ALS;
   INFILE ALSFILE;
   DROP ALIAS ALIASKEY;
   FORMAT NEWIC $15. CISNEWIC $15.;
   INPUT @5     CUSTNO      $11.
         @89    ALIASKEY    $3.
         @91    ALIAS       $15.;
    IF ALIAS = '' THEN DELETE;
    IF ALIASKEY IN ('IC ','ML ','PL ','PP');
    IF LENGTH(ALIASKEY) LE 12;
    NEWIC=ALIASKEY||ALIAS;
    CISNEWIC=NEWIC;
RUN;
PROC SORT  DATA=ALS NODUPKEY; BY NEWIC;RUN;

DATA MAILFLAG;
   INFILE MAILFLAG;
   INPUT @1     NEWIC       $15.
         @16    OLDIC       $12.
         @28    CONSENTCARD $1.;
    CARDNEWIC = NEWIC;
    CARDOLDIC = OLDIC;
    IF CONSENTCARD IN ('Y','N');
RUN;
PROC SORT  DATA=MAILFLAG ;BY NEWIC ;RUN;

DATA MATCHMAIL;
   MERGE MAILFLAG(IN=A) ALS(IN=B); BY NEWIC;
   IF A;
RUN;
PROC SORT  DATA=MATCHMAIL; BY CUSTNO;RUN;

 DATA REMATCH GOTOLDIC NOMATCH;
   MERGE MATCHMAIL(IN=C) CIS(IN=D); BY CUSTNO;
   IF C;
   IF CUSTNO NE   '' THEN DO ;OUTPUT REMATCH;END;
   ELSE DO;
       IF OLDIC NOT = '' AND CUSTNO = '' THEN OUTPUT GOTOLDIC;
       IF OLDIC     = '' AND CUSTNO = '' THEN OUTPUT NOMATCH;
   END;
 RUN;
PROC SORT  DATA=REMATCH ; BY CUSTNO;RUN;

 DATA MATCHNEWIC;
   MERGE REMATCH(IN=E) CIS(IN=F); BY CUSTNO;
   IF E;
 RUN;
PROC SORT  DATA=MATCHNEWIC ; BY CUSTNO;RUN;

PROC SORT  DATA=CIS        ; BY OLDIC;RUN;
PROC SORT  DATA=GOTOLDIC   ; BY OLDIC;RUN;

 DATA MATCHOLDIC;
   MERGE GOTOLDIC(IN=G) CIS(IN=H); BY OLDIC;
   IF G;
 RUN;

 DATA ALLREC;
   SET MATCHOLDIC MATCHNEWIC NOMATCH;
    IF CUSTNO = ''
      THEN REASON = 'NO MATCHING CIS                                 ';
    IF ( CONSENTCARD = 'Y' AND CISCONSENT = 'Y')
    OR ( CONSENTCARD = 'N' AND CISCONSENT = 'N')
      THEN REASON = 'SAME CONSENT                                    ';
    IF (( CONSENTCARD = 'Y' AND CISCONSENT = 'N' AND CUSTNO NE '')
    OR ( CONSENTCARD = 'N' AND CISCONSENT = 'Y' AND CUSTNO NE ''))
    THEN DO;
        IF  CISNEWIC = CARDNEWIC AND CISOLDIC = CARDOLDIC
           THEN REASON = 'UPDATE CONSENT TO CIS                      ';
           ELSE
                REASON = 'EXCEPTION. CONSENT / ID DIFFERENCE         ';
    END;
    IF ( CONSENTCARD = 'Y' AND CISCONSENT = ' ' AND CUSTNO NE '')
    OR ( CONSENTCARD = 'N' AND CISCONSENT = ' ' AND CUSTNO NE '')
      THEN REASON = 'UPDATE CONSENT TO CIS                           ';

    BRANCH = '001';
 RUN;
 PROC PRINT DATA=ALLREC(OBS=50);TITLE 'ALL REC';RUN;

PROC SORT  DATA=ALLREC; BY REASON NEWIC OLDIC;RUN;
DATA OUTPUTALL; /* LISTING FOR CHECKING */
    SET ALLREC;
    FILE OUTFILE;
    IF _N_ = 1 THEN DO;
    PUT @001 'DAILY    UPDATE';
    PUT @001 'CARDNEWIC'
        @018 'CARDOLDIC'
        @035 'CISNEWIC'
        @052 'CISOLDIC'
        @069 'M'
        @072 'C'
        @075 'CUSTNO'
        @088 'REASON'              ;
        END;
    PUT @001 CARDNEWIC      $15.
        @018 CARDOLDIC      $15.
        @035 CISNEWIC       $15.
        @052 CISOLDIC       $15.
        @069 CONSENTCARD    $1.
        @072 CISCONSENT     $1.
        @075 CUSTNO         $11.
        @088 REASON         $48. ;
RUN;

DATA UPDATE_CUST;  /* FILE TO PERFORM UPDATE CUSTOMER MSCODE 7*/
    SET ALLREC;
    FILE UPDCUST;
    IF REASON = 'UPDATE CONSENT TO CIS';
    IF CONSENTCARD = 'Y' THEN MISCDEMO7 = '001';
    IF CONSENTCARD = 'N' THEN MISCDEMO7 = '002';
    PUT @001 '033'
        @004 CUSTNO         $20.
        @024 INDORG         $1.
        @025 MISCDEMO7      $10. ;
RUN;

DATA UPDATE_CON1;  /* FILE TO PERFORM UPDATE CICON1ST*/
    SET ALLREC;
    FILE UPDCON1;
    IF REASON = 'UPDATE CONSENT TO CIS';
    PUT @001 '033'                       /* BANKNO         */
        @004 'CUST'                      /* APPLCODE       */
        @009 CUSTNO               $20.   /* APPLNO         */
        @029 CONSENTCARD          $01.   /* CONSENT        */
        @030 INDORG               $01.   /* CUST-TYPE      */
        @031 "&DATE1"                    /* FIRST-DATE     */
        @041 '0'                         /* NO-OF-PROMPT   */
        @042 'UNI'                       /* PROMPTSOURCE   */
        @047 "&DATE1"                    /* PROMPTDATE     */
        @057 '01.01.01'                  /* PROMPTTIME     */
        @065 'UNI'                       /* UPDATESOURCE   */
        @070 "&DATE1"                    /* UPDATEDATE     */
        @080 '01.01.01'                  /* UPDATETIME     */
        @088 'UNIBATCH'                  /* UPDATEOPERATOR */
        @096 ' '                         /* TRXAPPLCODE    */
        @101 ' '                      ;  /* TRXAPPLNO      */

RUN;

PROC SORT  DATA=ALLREC; BY BRANCH REASON NEWIC OLDIC;RUN;
 /*----------------------------------------------------------------*/
 /*   OUTPUT DETAIL REPORT                                         */
 /*----------------------------------------------------------------*/
DATA _NULL_;
  IF TRN=0 THEN DO;
     BRANCH = '001';
     FILE EXCEPT  PRINT HEADER=NEWPAGE;
     PUT _PAGE_;
     PUT  /@044   '**********************************';
     PUT  /@044   '*                                *';
     PUT  /@044   '*       NO ERROR RECORDS         *';
     PUT  /@044   '*                                *';
     PUT  /@044   '**********************************';
  END;

  RETAIN TRN;

  SET ALLREC   NOBS=TRN END=EOF;BY BRANCH ;
  FILE EXCEPT  PRINT HEADER=NEWPAGE NOTITLE;
  LINECNT = 0;
  FORMAT BANKNAME $45.;

  IF LINECNT >= 40 OR LAST.BRANCH THEN DO;
     PUT _PAGE_;
  END;

    LINECNT + 5;

  IF BRANCH NE SPACES THEN DO;
     LINECNT + 1;
     BRCUST   + 1;
     PUT @1    'UNICARD   '
         @12   ' '                 /* FILLER */
         @25   NEWIC          $15.
         @42   OLDIC          $15.
         @62   CONSENTCARD    $1.
         @82   REASON         $48. ;
     PUT @1    'CIS       '
         @12   CUSTNO         $11.
         @25   CISNEWIC       $15.
         @42   TAXID          $15.
         @62   CISCONSENT     $1. ;
     END;

  IF LAST.BRANCH THEN DO;
     PUT  @1  'TOTAL RECORDS = '
          @23  BRCUST      7.;

          BRCUST  = 0;
          PAGECNT = 0;
  END;

  RETURN;

  NEWPAGE :
    PAGECNT+1;
    LINECNT = 0;

    BANKNAME = 'PUBLIC BANK BERHAD';
    PUT @1   'REP:    MAILFLAGC         '
        @55   BANKNAME
        @79  'REPORT DATE : ' "&DATE2"
       /@1   'PROGRAM ID  : CICONMAI'
        @79  'PAGE        : ' PAGECNT   4.
       /@1   'BRANCH      : '  BRANCH    $7.
        @43  'CUSTOMER DATA CONSENT DAILY EXCEPTION (UNICARD TO CIS)'
       /@43  '======================================================';

    PUT   /@1    'SOURCE'
           @12   'CUSTNO'
           @25   'NEWIC'
           @42   'OLDIC'
           @62   'CONSENT'
           @82   'REASON' ;
    PUT    @1    '======'
           @12   '==========='
           @25   '==============='
           @42   '==============='
           @62   '======='
           @82   '======'    ;

    LINECNT = 9;
  RETURN;
RUN;
