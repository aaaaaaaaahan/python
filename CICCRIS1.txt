//CICCRIS1 JOB MSGCLASS=X,MSGLEVEL=(1,1),REGION=8M,NOTIFY=&SYSUID       JOB54848
//*--------------------------------------------------------------------
//INITDASD EXEC PGM=IEFBR14
//DEL2     DD DSN=RBP2.B033.CIS.CCRIS.ERROR,
//            DISP=(MOD,DELETE,DELETE),UNIT=SYSDA,SPACE=(TRK,(0))
//*--------------------------------------------------------------------
//STATS#01 EXEC SAS609
//UNQACCT  DD DISP=SHR,DSN=RBP2.B033.ACTIVE.ACCOUNT.UNIQUE
//CISFILE  DD DISP=SHR,DSN=RBP2.B033.CIS.CUST.DAILY
//ERRFILE  DD DSN=RBP2.B033.CIS.CCRIS.ERROR,
//            DISP=(NEW,CATLG,DELETE),
//            SPACE=(CYL,(200,100),RLSE),UNIT=SYSDA,
//            DCB=(LRECL=200,BLKSIZE=0,RECFM=FB)
//SASLIST  DD SYSOUT=X
//SYSIN    DD *

OPTIONS IMSDEBUG=N YEARCUTOFF=1950 SORTDEV=3390 ERRORS=0;
OPTIONS NODATE NONUMBER NOCENTER;
TITLE;
 DATA ACCT;
    INFILE UNQACCT;
    INPUT @1    CUSTNO        $11.
          @12   ACCTCODE      $5.
          @17   ACCTNOC       $11.
          @37   NOTENO        $5.
          @42   ALIASKEY      $3.
          @45   ALIAS         $15.
          @60   CUSTNAME      $40.
          @100  BRANCH        $5.
          @106  OPENDATE      $8.
          @115  OPENIND       $1.;
 PROC SORT  DATA=ACCT; BY CUSTNO ACCTNOC;
 PROC PRINT DATA=ACCT(OBS=5);TITLE 'UNIQUE ACCOUNT';RUN;

 DATA CIS OUT1 OUT2 OUT3 OUT4 OUT5 ;
    FORMAT FIELDTYPE $20. FIELDVALUE  $30. REMARKS $40. ERRORCODE $3.;
    SET CISFILE.CUSTDLY;
    IF CUSTNAME = '' THEN DELETE;
    IF ACCTCODE IN ('DP','LN');

    /* ERRORCODE 001 - CHECK FOR EMPTY CITIZENSHIP */
    IF CITIZENSHIP IN ('OT','','99') THEN DO;
       FIELDTYPE = 'CITIZENSHIP';
       FIELDVALUE = CITIZENSHIP;
       IF CITIZENSHIP EQ ''   THEN FIELDVALUE = 'BLANK CITIZENSHIP';
       IF CITIZENSHIP EQ 'OT' THEN FIELDVALUE = 'OTHER CITIZENSHIP';
       IF CITIZENSHIP EQ '99' THEN FIELDVALUE = 'UNKNOWN CITZNSHIP';
       REMARKS = 'PLS MAINTAIN CITIZENSHIP';
       ERRORCODE = '001';
       OUTPUT OUT1;
    END;

    /* ERRORCODE 002 - CHECK FOR EMPTY INDIVIDUAL ID */
    IF INDORG = 'I' AND
       ALIASKEY NOT IN ('IC','PP','ML','PL','BC') THEN DO;
       FIELDTYPE = 'ALIAS TYPE ';
       FIELDVALUE = ALIASKEY||ALIAS;
       IF ALIASKEY EQ '' THEN FIELDVALUE = 'BLANK ID (INDV CUST)';
       REMARKS = 'PLS MAINTAIN INDIVIDUAL ID';
       ERRORCODE = '002';
       OUTPUT OUT2;
    END;

    /* ERRORCODE 003 - CHECK FOR EMPTY ORGANISATION ID */
    IF INDORG = 'O' AND
       ALIASKEY NOT IN ('BR','CI','PC','SA') THEN DO;
       FIELDTYPE = 'ALIAS TYPE ';
       FIELDVALUE = ALIASKEY||ALIAS;
       IF ALIASKEY EQ '' THEN FIELDVALUE = 'BLANK ID (ORG CUST)';
       REMARKS = 'PLS MAINTAIN ORGANISATION ID';
       ERRORCODE = '003';
       OUTPUT OUT3;
    END;

    /* ERRORCODE 004 - CHECK FOR EMPTY DATE OF BIRTH INDIVIDUAL */
    IF DOBCC = '' AND INDORG = 'I' THEN DO;
       FIELDTYPE = 'DATE OF BIRTH';
       FIELDVALUE = 'BLANK DOB';
       REMARKS = 'PLS MAINTAIN DATE OF BIRTH';
       ERRORCODE = '004';
       OUTPUT OUT4;
    END;

    /* ERRORCODE 005 -CHECK FOR EMPTY DATE OF REGISTER ORGANISATION */
    IF DOBCC = '' AND INDORG = 'O' THEN DO;
       FIELDTYPE = 'BUS. SINCE ';
       FIELDVALUE = 'BLANK DOR';
       REMARKS = 'PLS MAINTAIN BUS. SINCE DATE';
       ERRORCODE = '005';
       OUTPUT OUT5;
    END;
 RUN ; /* END OF READING CIS CUST DAILY FILE */

 PROC PRINT DATA=CIS(OBS=5);TITLE 'CIS';RUN;

 DATA ERROREC;
      SET OUT1 OUT2 OUT3 OUT4 OUT5 ;
 RUN;
 PROC SORT  DATA=ERROREC; BY CUSTNO ACCTNOC;

 DATA MERGE;
      MERGE ERROREC(IN=A) ACCT(IN=B); BY CUSTNO ACCTNOC;
      IF A AND B;
 RUN;

DATA _NULL_;
  SET MERGE;
  FILE ERRFILE;
     IF PRISEC = 901 THEN PRIMSEC = 'P';
     IF PRISEC = 902 THEN PRIMSEC = 'S';
     PUT @01   BRANCH            $5.
         @06   ACCTCODE          $5.
         @11   ACCTNOC           $20.
         @31   PRIMSEC           $1.
         @32   CUSTNO            $11.
         @43   ERRORCODE         $03.
         @46   FIELDTYPE         $20.
         @66   FIELDVALUE        $30.
         @96   REMARKS           $40.;
  RETURN;
  RUN;
