//CICARMRP JOB MSGCLASS=X,MSGLEVEL=(1,1),REGION=8M,NOTIFY=&SYSUID       JOB34311
//**********************************************************************
//*- GENERATE MONTHLY MATCHING EXCEPTION UNOFAC REPORT
//**********************************************************************
//REPORT    EXEC SAS609
//CTRLDATE  DD DISP=SHR,DSN=RBP2.B033.SRSCTRL1(0)
//INFILE    DD DISP=SHR,DSN=RBP2.B033.UNOFAC.CARD.MTHLY
//OUTFILE   DD DSN=RBP2.B033.UNOFAC.CARD.MTLRPT(+1),
//             DISP=(NEW,CATLG,DELETE),UNIT=SYSDA,
//             SPACE=(CYL,(10,5),RLSE),
//             DCB=(LRECL=134,BLKSIZE=0,RECFM=FBA)
//SASLIST   DD SYSOUT=X
//SYSIN     DD *
OPTIONS YEARCUTOFF=1950 SORTDEV=3390 NONUMBER NODATE NOCENTER;

 /*----------------------------------------------------------------*/
 /*    SET DATES                                                   */
 /*----------------------------------------------------------------*/
DATA SRSDATE;
   INFILE CTRLDATE;
     INPUT @001  SRSYY    4.
           @005  SRSMM    2.
           @007  SRSDD    2.;

   REPTDATE=MDY(SRSMM,SRSDD,SRSYY);
   CALL SYMPUT('RDATE',PUT(REPTDATE,DDMMYY10.));
RUN;
PROC PRINT;RUN;

DATA RDATA ;
   INFILE INFILE;
   INPUT @01   BANKNO             $3.
         @04   ACTOPND            $10.
         @14   BRANCH              5.
         @19   APPLCODE           $5.
         @24   ACCTNO             $16.
         @40   INDORG             $1.
         @41   PRIMNAME           $40.
         @81   SECNAME            $40.
         @121  ALIASKEY           $3.
         @124  ALIAS              $12.
         @136  TAXID              $12.
         @148  IO                 $1.
         @149  NAME               $40.
         @189  NEWIC              $40.
         @229  OTHERID            $40.
         @269  SOURCE             $4.
         @273  LSTMNTDATE         $20.
         @293  REMARK1            $40.
         @333  REMARK2            $40.
         @373  REMARK3            $40.
         @413  REMARK4            $40.
         @453  REMARK5            $40.
         @493  REMARK6            $40.
         @533  REMARK7            $40.
         @573  REMARK8            $40.
         @613  REMARK9            $40.
         @653  REMARK10           $40.
         @693  CTDATE             $20.
         @713  MATCHBY            $4.
         @717  DISPRMK            $8.;
*;
PROC SORT DATA=RDATA; BY BRANCH MATCHBY ACCTNO APPLCODE;
RUN;
DATA REPORT;
  FILE OUTFILE PRINT HEADER=NEWPAGE;
  IF TRN=0 THEN DO;
     PUT @002 ' ';
     PUT @047 '**********************************';
     PUT @047 '*                                *';
     PUT @047 '*         EMPTY REPORT           *';
     PUT @047 '*                                *';
     PUT @047 '**********************************';
     PUT @002 ' ';
  END;

  RETAIN TRN;

   SET RDATA NOBS=TRN END=EOF; BY BRANCH MATCHBY ACCTNO APPLCODE;
   ARRAY GRDTOT(3) 8. GRDTOT1-GRDTOT3;
   FILE OUTFILE NOTITLE;

   IF _N_ = 1 THEN DO;
      PAGECNT = 0;
      DO I = 1 TO 3;
         GRDTOT(I) = 0;
      END;
   END;

   IF LINECNT >= 55 OR FIRST.BRANCH THEN DO;
      PUT _PAGE_;
   END;

   IF FIRST.MATCHBY THEN DO;
      IF MATCHBY = 'ICD ' THEN DO;
        PUT @001 'Customer with NAME AND ID fully matched ';
        PUT @001 '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~';
      END;
      IF MATCHBY = 'NAME' THEN DO;
        PUT @001 'Customer NAME matched but ID unmatched/unavailable '
            @052 'OR customer ID matched but NAME unmatched';
        PUT @001 '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~'
            @052 '~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~';
      END;
   END;

   IF FIRST.ACCTNO THEN DO;
      IF ALIAS = ' ' OR (TAXID NE ' ' AND OTHERID = TAXID) THEN DO;
          PUT @002 PRIMNAME     $40.
              @046 TAXID        $10.
              @070 ACTOPND      $10.
              @084 APPLCODE     $5.
              @098 ACCTNO       $20.
              @118 DISPRMK      $8.;
      END;
      ELSE DO;
          PUT @002 PRIMNAME     $40.
              @046 ALIAS        $20.
              @070 ACTOPND      $10.
              @084 APPLCODE     $5.
              @098 ACCTNO       $20.
              @118 DISPRMK      $8.;
      END;
      LINECNT + 1;
      IF MATCHBY = 'ICD ' THEN DO;
          PUT @005 NAME         $40.
              @050 NEWIC        $14.
              @067 OTHERID      $40.
              @118 SOURCE       $8.;
      END;
      IF  INDORG = 'I' THEN DO;GRDTOT(1) +1;END;
      IF  INDORG = 'O' THEN DO;GRDTOT(2) +1;END;
      GRDTOT(3) +1;END;
   ELSE DO;
      IF MATCHBY = 'ICD ' THEN DO;
          PUT @005 NAME         $40.
              @050 NEWIC        $14.
              @067 OTHERID      $40.
              @118 SOURCE       $8.;
      END;
   END;

   IF LINECNT > 55 THEN DO; LINK NEWPAGE; LINECNT=0; END;

   IF EOF THEN DO;
      PUT @002 ' ';
      PUT @002 'TOTAL NUMBER OF RECORDS (INDIVIDUAL)   : ' GRDTOT(1);
      PUT @002 'TOTAL NUMBER OF RECORDS (ORGANISATION) : ' GRDTOT(2);
      PUT @002 'GRAND TOTAL OF RECORD TO BE MATCH      : ' GRDTOT(3);
   END;
   RETURN;

   NEWPAGE:
      PAGECNT +1;
      LINECNT = 0;
      PUT @1   'REPORT NO : UN/OFAC/CARD/ALL/ACCT'
          @51  'P U B L I C  B A N K  B H D'
          @110 'PAGE'
          @121 ':'
          @122 PAGECNT 6.;
      PUT @1   'PROGRAM ID: CICARMRP'
          @47  'MONTHLY UNICARD EXCEPTION REPORT FOR OFAC & UN LIST'
          @110 'REPORT DATE: ' "&RDATE";
      PUT @1   'BRANCH NO : ' BRANCH;
      PUT @002 'NAME'
          @046 'ID NO.'
          @070 'DATE OPEN'
          @084 'CARD TYPE'
          @098 'CARD NO.'
          @118 'REMARKS.';
      PUT @002 '========================================'
          @046 '===================='
          @070 '=========='
          @084 '=========='
          @098 '================'
          @118 '========';
      PUT @005 'UN/OFAC NAME'
          @050 'UN/OFAC IC NO'
          @067 'UN/OFAC OTHER ID';
      PUT @005 '------------'
          @050 '-------------'
          @067 '----------------';
      LINECNT +7;
   RETURN;
RUN;

*;
//*********************************************************************
//*PRINT THE RESULT (OUTPUT)
//*********************************************************************
//COPYHOR1 EXEC PGM=IEBGENER
//SYSPRINT DD SYSOUT=X
//SYSUT1   DD DISP=SHR,DSN=RBP2.B033.UNOFAC.CARD.MTLRPT(+1)
//SYSUT2   DD SYSOUT=X
//SYSIN    DD DUMMY
