//CIHRCRVP JOB MSGCLASS=X,MSGLEVEL=(1,1),REGION=64M,NOTIFY=&SYSUID      J0196022
//*--------------------------------------------------------------------
//**ESMR 2023-1008 DB2 FOR HRC - PERIODIC REVIEW
//**ESMR 2023-1473 DB2 FOR HRC - ADDITIONAL COLUMNS
//**ESMR 2023-2612 CLARIFICATION FOR 30% RANDOM FILTER
//**ESMR 2025-3264 FUZZY LOGIC PERIODIC REVIEW: REVISE THE SYSTEM LOGIC
//**               FOR 30% SAMPLING BASED ON THE FUZZY LOGIC SCORING
//**
//*--------------------------------------------------------------------
//INITDASD EXEC PGM=IEFBR14
//DEL3     DD DSN=RBP2.B033.CIHRCRVP.INIT,
//            DISP=(MOD,DELETE,DELETE),UNIT=SYSDA,SPACE=(TRK,(0))
//DEL4     DD DSN=RBP2.B033.CIHRCRVP.UPDATE,
//            DISP=(MOD,DELETE,DELETE),UNIT=SYSDA,SPACE=(TRK,(0))
//*--------------------------------------------------------------------
//SAMPLING EXEC SAS609
//CIULHRCR DD DISP=SHR,DSN=RBP2.B033.UNLOAD.CIHRCRVT.FB
//CTRLDATE DD DISP=SHR,DSN=RBP2.B033.SRSCTRL1(0)
//SASLIST  DD SYSOUT=X
//SORTWK01 DD UNIT=SYSDA,SPACE=(CYL,(200,100))
//SORTWK02 DD UNIT=SYSDA,SPACE=(CYL,(200,100))
//INITFILE DD DSN=RBP2.B033.CIHRCRVP.INIT,
//            DISP=(NEW,CATLG,DELETE),
//            SPACE=(CYL,(200,200),RLSE),UNIT=SYSDA,
//            DCB=(LRECL=100,BLKSIZE=0,RECFM=FB)
//UPDFILE  DD DSN=RBP2.B033.CIHRCRVP.UPDATE,
//            DISP=(NEW,CATLG,DELETE),
//            SPACE=(CYL,(200,200),RLSE),UNIT=SYSDA,
//            DCB=(LRECL=100,BLKSIZE=0,RECFM=FB)
//SYSIN    DD *
OPTIONS NODATE NONUMBER NOCENTER;
TITLE;
 /*----------------------------------------------------------------*/
 /*    SET DATES                                                   */
 /*----------------------------------------------------------------*/
     DATA SRSDATE;
           INFILE CTRLDATE;
             INPUT @001  TODAYSAS   YYMMDD8.;

           /* DISPLAY TODAY REPORTING DATE*/
           CALL SYMPUT('DATE1',TODAYSAS);
     RUN;
     PROC PRINT DATA=SRSDATE  ;TITLE 'SRSDATE ';RUN;

 /*----------------------------------------------------------------*/
 /*    INPUT FILE DATA DECLARATION                                 */
 /*----------------------------------------------------------------*/
 DATA ALLREC;
    INFILE CIULHRCR ;
        INPUT  @  1  HRV_MONTH               $6.
               @  7  HRV_BRCH_CODE           $7.
               @ 14  HRV_ACCT_TYPE           $5.
               @ 19  HRV_ACCT_NO             $20.
               @ 39  HRV_CUSTNO              $20.
               @ 59  HRV_CUSTID              $40.
               @ 99  HRV_CUST_NAME           $120.
               @219  HRV_NATIONALITY         $2.
               @221  HRV_ACCT_OPENDATE       YYMMDD10.
               @231  HRV_OVERRIDING_INDC     $1.
               @232  HRV_OVERRIDING_OFFCR    $10.
               @242  HRV_OVERRIDING_REASON   $100.
               @342  HRV_DOWJONES_INDC       $1.
               @343  HRV_FUZZY_INDC          $1.
               @344  HRV_FUZZY_SCORE         PD3.2
         /*    @347  HRV_NOTED_BY            $10.
               @357  HRV_RETURNED_BY         $10.
               @367  HRV_ASSIGNED_TO         $10.
               @377  HRV_NOTED_DATE          $26.
               @403  HRV_RETURNED_DATE       $26.
               @429  HRV_ASSIGNED_DATE       $26.
               @455  HRV_COMMENT_BY          $10.
               @465  HRV_COMMENT_DATE        $26.
               @491  HRV_SAMPLING_INDC       $1.
               @492  HRV_RETURN_STATUS       $1.
               @493  HRV_RECORD_STATUS       $1.
               @494  HRV_FUZZY_SCREEN_DATE   $10. */
               ;

      /*SMR 2023-1473 URD 3.2 PAGE 1*/
      IF HRV_ACCT_OPENDATE   =  &DATE1  ;         /*URD 3.2.5*/
      IF HRV_FUZZY_INDC      = 'Y';               /*URD 3.2.1*/
      IF HRV_OVERRIDING_INDC IN (' ','N');        /*URD 3.2.2*/
      IF HRV_BRCH_CODE = '996    ' THEN DELETE;   /*URD 3.2.4*/
 RUN;
 PROC SORT  DATA=ALLREC ; BY DESCENDING HRV_FUZZY_SCORE;RUN;
 PROC PRINT DATA=ALLREC(OBS=50);TITLE 'ALL ACCT';RUN;

 /*----------------------------------------------------------*/
 /* THIS DATASTEP WILL SPLIT THE RECORD BASED ON THE SCORE   */
 /*----------------------------------------------------------*/
 DATA HIGHSCORE LOWSCORE;
      SET ALLREC;
      IF HRV_FUZZY_SCORE > 89 THEN OUTPUT HIGHSCORE;
      IF HRV_FUZZY_SCORE <= 89 THEN OUTPUT LOWSCORE;
 RUN;
 PROC SORT  DATA=HIGHSCORE; BY DESCENDING HRV_FUZZY_SCORE;RUN;
 PROC PRINT DATA=HIGHSCORE(OBS=20);TITLE 'HIGHSCORE';RUN;
 PROC SORT  DATA=LOWSCORE; BY DESCENDING HRV_FUZZY_SCORE;RUN;
 PROC PRINT DATA=LOWSCORE(OBS=20);TITLE 'LOWSCORE';RUN;

 /*----------------------------------------------------------*/
 /* THIS DATASTEP WILL COUNT TOTAL RECORD AND TOTAL SAMPLING */
 /*----------------------------------------------------------*/
 DATA A;
    SET ALLREC NOBS=TOT_ALL ;
    CALL SYMPUT('TOTAL_ALL_RECORD',TOT_ALL);
 RUN;

 DATA A1;
    SET HIGHSCORE NOBS=TOT_HIGH;
    CALL SYMPUT('TOTAL_HIGH',TOT_HIGH);
 RUN;

 DATA B;
    TAKE_SAMP = &TOTAL_ALL_RECORD * 0.3 ;      /* 30% SAMPLING */
    TAKE_SAMP = ROUND(TAKE_SAMP);
    CALL SYMPUT('TOTAL_SAMPLING',TAKE_SAMP);
 RUN;

 /*------------------------------------------------------*/
 /* COUNT IF HIGHSCORE >30% OF THE RECORDS               */
 /*------------------------------------------------------*/
 DATA COUNT;
    PCT_HIGH = (&TOTAL_HIGH/&TOTAL_ALL_RECORD) * 100;
    PCT_HIGH = ROUND(PCT_HIGH);
    CALL SYMPUT('PCT_HIGH',PCT_HIGH);
 RUN;
 PROC PRINT DATA=COUNT;TITLE 'COUNT';RUN;

 /*------------------------------------------------------*/
 /* FINAL OUTPUT BASED ON PERCENTAGE OF HIGHSCORE        */
 /*------------------------------------------------------*/

 DATA FINAL;
    IF &PCT_HIGH >= 30 THEN SET HIGHSCORE;
    ELSE SET ALLREC(OBS=&TOTAL_SAMPLING);
 RUN;
 /*------------------------------------------------------*/
 /* OUTPUT FILE                                          */
 /*------------------------------------------------------*/
 DATA INIT   ;        /* TO INITIALISE RECORDS IN THE MONTH*/
    FILE INITFILE;
    SET  FINAL (OBS=1);
    PUT  @  1  HRV_MONTH               $6.
         @  7  HRV_BRCH_CODE           $7.
         @ 14  HRV_ACCT_TYPE           $5.
         @ 19  HRV_ACCT_NO             $20.
         @ 39  HRV_CUSTNO              $20.
         @ 59  HRV_ACCT_OPENDATE       YYMMDD10. /* INIT THIS ONLY */
         ;
 RUN;

 DATA UPDATE ;        /* TO UPDATE INDICATOR FOR DATA SAMPLING*/
    FILE UPDFILE;
    SET  FINAL;
    PUT  @  1  HRV_MONTH               $6.
         @  7  HRV_BRCH_CODE           $7.
         @ 14  HRV_ACCT_TYPE           $5.
         @ 19  HRV_ACCT_NO             $20.
         @ 39  HRV_CUSTNO              $20.
         @ 59  HRV_ACCT_OPENDATE       YYMMDD10.
         ;
 RUN;
