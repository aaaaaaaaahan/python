//CIINQTRY JOB MSGCLASS=X,MSGLEVEL=(1,1),REGION=64M,NOTIFY=&SYSUID      J0086594
//*--------------------------------------------------------------------
//*- EXTRACT INQUIRY LOG FOR TREASURY
//*- ESMR 2020-3695
//*--------------------------------------------------------------------
//*-STEP 2 - PRINT OUT THE DATASET INTO A REPORT
//*-------------------------------------------------------------------
//STATS#01 EXEC SAS609
//SORTWK01  DD UNIT=SYSDA,SPACE=(CYL,(1000,500))
//SORTWK02  DD UNIT=SYSDA,SPACE=(CYL,(1000,500))
//SORTWK03  DD UNIT=SYSDA,SPACE=(CYL,(1000,500))
//SORTWK04  DD UNIT=SYSDA,SPACE=(CYL,(1000,500))
//CTRLDATE  DD DISP=SHR,DSN=RBP2.B033.SRSCTRL1(0)
//LOGFILE   DD DISP=SHR,DSN=RBP2.B033.UNLOAD.CIINQLG2.FB
//NAMEFILE  DD DISP=SHR,DSN=RBP2.B033.UNLOAD.PRIMNAME.OUT
//DPTRBALS  DD DISP=SHR,DSN=RBP2.B033.DPTRBLGS
//OUTFILE   DD DSN=RBP2.B033.UNLOAD.CIINQLGT.RPTSRY(+1),
//             DISP=(NEW,CATLG,DELETE),
//             SPACE=(CYL,(100,100),RLSE),UNIT=SYSDA,
//             DCB=(LRECL=133,BLKSIZE=0,RECFM=FBA)
//SASLIST   DD SYSOUT=X
//SYSIN     DD *
 /*----------------------------------------------------------------*/
 /*    SET DATES                                                   */
 /*----------------------------------------------------------------*/
       DATA SRSDATE;
             INFILE CTRLDATE;
               INPUT @001  SRSYY    4.
                     @005  SRSMM    2.
                     @007  SRSDD    2.;

             /* DISPLAY TODAY REPORTING DATE*/
             TODAYSAS=MDY(SRSMM,SRSDD,SRSYY);
             CALL SYMPUT('DATE1',PUT(TODAYSAS,YYMMDD10.));
       RUN;

 /*----------------------------------------------------------------*/
 /*    DEPOSIT TRIAL BALANCE                                       */
 /*----------------------------------------------------------------*/
  DATA DPTRBALS;
     INFILE DPTRBALS ;

     INPUT @03  BANKNO       PD2.
           @24  REPTNO       PD3.
           @27  FMTCODE      PD2. @;

     IF (REPTNO = 1001 AND (FMTCODE IN (1,10,22,019,020,021))) THEN DO;
        INPUT @110  ACCTNO    PD6.
              @116  ACCTNAME  $15.;
     APPL_NO = PUT(ACCTNO,Z11.);
     END;
  RUN;
  PROC SORT  DATA=DPTRBALS NODUPKEY; BY APPL_NO;RUN;

 /*----------------------------------------------------------------*/
 /*    DEPOSIT TRIAL BALANCE                                       */
 /*----------------------------------------------------------------*/
DATA NAME;
   INFILE NAMEFILE;
       INPUT @005   CUSTNO          $11.
             @089  CUSTNAME         $40.;
RUN;
PROC SORT  DATA=NAME NODUPKEY; BY CUSTNO ;RUN;

DATA CISLOG ;
   INFILE LOGFILE;
       INPUT @001   TERMINAL_ID     $08.
             @009   INQ_DATE        $10.
             @009   INQ_DATE_YY     $4.
             @014   INQ_DATE_MM     $2.
             @017   INQ_DATE_DD     $2.
             @019   INQ_TIME        $10.
             @029   OPERATOR        $10.
             @039   APPL_CODE       $05.
             @044   APPL_NO         $20.
             @044   CUSTNO          $20.
             @064   APPL_NAME       $50.       /* NAME OR IC */
             @114   SCREEN_ID       $08.
             @122   SCREEN_DESC     $30. ;
             IF CUSTNO = 'SEARCH BY NAME/ALIAS' THEN
                         CUSTNAME = APPL_NAME;
             IF APPL_NO       = 'SEARCH BY TAX ID' THEN
                         CUSTNAME = APPL_NAME;
             IF U_INQ_APPL_NO = 'SEARCH BY PHONE ' THEN
                         CUSTNAME = APPL_NAME;
   RUN;

DATA CUSTLOG ACCTLOG;    /* CONTAINS WORK FOR SET XXXX ONLY */
    SET CISLOG;
    WHERE   ((OPERATOR  CONTAINS 'B752CWY'      )
    OR       (OPERATOR  CONTAINS 'B752TSF'      )
    OR       (OPERATOR  CONTAINS 'B752CKS'      )
    OR       (OPERATOR  CONTAINS 'B752QYS'      ));
    IF APPL_CODE EQ 'CUST ' THEN OUTPUT CUSTLOG;
    IF APPL_CODE NE 'CUST ' THEN OUTPUT ACCTLOG;
RUN;

PROC SORT  DATA=ACCTLOG      ;
               BY APPL_NO;RUN;

DATA MERGE_ACCTNAME;
     MERGE ACCTLOG(IN=X)  DPTRBALS(IN=Y); BY APPL_NO;
     IF X;
     IF APPL_CODE = 'DP   ' THEN CUSTNAME = ACCTNAME;
     IF APPL_CODE = 'DP   ' AND CUSTNAME = '' THEN DELETE;
RUN;

PROC SORT  DATA=CUSTLOG      ; BY CUSTNO ;RUN;
PROC SQL;
CREATE TABLE MERGENAME AS
  SELECT CUSTLOG.TERMINAL_ID
        ,CUSTLOG.INQ_DATE
        ,CUSTLOG.INQ_DATE_YY
        ,CUSTLOG.INQ_DATE_MM
        ,CUSTLOG.INQ_DATE_DD
        ,CUSTLOG.INQ_TIME
        ,CUSTLOG.OPERATOR
        ,CUSTLOG.APPL_CODE
        ,CUSTLOG.APPL_NO
        ,CUSTLOG.CUSTNO
        ,CUSTLOG.APPL_NAME
        ,CUSTLOG.SCREEN_ID
        ,CUSTLOG.SCREEN_DESC
        ,NAME.CUSTNAME
  FROM CUSTLOG,NAME
  WHERE CUSTLOG.CUSTNO = NAME.CUSTNO;
QUIT;


PROC SORT  DATA=MERGENAME    ;
               BY INQ_DATE  INQ_TIME APPL_CODE APPL_NO;RUN;

PROC SORT  DATA=MERGE_ACCTNAME    ;
               BY INQ_DATE  INQ_TIME APPL_CODE APPL_NO;RUN;

DATA OUT1;
  SET MERGENAME MERGE_ACCTNAME;
               BY INQ_DATE  INQ_TIME APPL_CODE APPL_NO;
  FILE OUTFILE;
        IF CUSTNAME = '' AND APPL_NO = '' THEN DELETE;
        IF SUBSTR(CUSTNAME,1,1) = '00'X THEN DELETE;
        PUT @1     OPERATOR     $10.
            @11    CUSTNAME     $40.
            @55    APPL_CODE    $5.
            @60    APPL_NO      $20.
            @81    'CIS'
            @91    INQ_DATE     $10.
            @101   INQ_TIME     $8. ;

RUN;
