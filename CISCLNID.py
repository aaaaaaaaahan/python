//CISCLNID JOB MSGCLASS=X,MSGLEVEL=(1,1),REGION=64M,NOTIFY=&SYSUID      J0065051
//*---------------------------------------------------------------------
//* ESMR 2013-2555 SCEL : LOAN ACCOUNT NUMBER AND ALIASES FOR CUSTOMER
//* ESMR 2013-2900 SCEL REVAMP DB
//* ESMR 2014-1612 ADD PRIMSEC INDICATOR (P/S)
//* ESMR 2015-2542 INCLUDE 3 NEW INDICATOR
//*                 /* HIRER      CA 020*/
//*                 /* GUARANTOR  CA 017*/
//*                 /* PARTNER    CC 050*/
//*---------------------------------------------------------------------
//*-ESMR2019-4587 NEW SA ACCT RANGE WITH 5 SERIES
//*-TAG :- 194587
//*---------------------------------------------------------------------
//*-ESMR 2023-370 AUTO ISSUANCE OF AKPK PRE BANKRUPTCY NOTICE
//*-ADD BASICGRPCODE TO OUTPUT FILE
//*---------------------------------------------------------------------
//DELETE   EXEC PGM=IEFBR14
//DEL1     DD DSN=RBP2.B033.SCEL.LOAN.IDS,
//            DISP=(MOD,DELETE,DELETE),SPACE=(TRK,(0))
//*---------------------------------------------------------------------
//*-EXTRACT LOAN & COLLATERAL ACCOUNT NUMBER TOGETHER WITH ALIAS
//*---------------------------------------------------------------------
//MOVEDATA EXEC SAS609
//IEFRDER   DD DUMMY
//SORTWK01  DD UNIT=SYSDA,SPACE=(CYL,(500,500))
//SORTWK02  DD UNIT=SYSDA,SPACE=(CYL,(500,500))
//SORTWK03  DD UNIT=SYSDA,SPACE=(CYL,(500,500))
//SORTWK04  DD UNIT=SYSDA,SPACE=(CYL,(500,500))
//SORTWK05  DD UNIT=SYSDA,SPACE=(CYL,(500,500))
//CISFILE   DD DISP=SHR,DSN=RBP2.B033.CIS.CUST.DAILY
//IDFILE    DD DISP=SHR,DSN=RBP2.B033.CIS.CUST.DAILY.IDS
//CCFILE    DD DISP=SHR,DSN=RBP2.B033.CCRIS.CC.RLNSHIP.SRCH
//OUTFILE   DD DSN=RBP2.B033.SCEL.LOAN.IDS,
//             DISP=(NEW,CATLG,DELETE),
//             UNIT=SYSDA,SPACE=(CYL,(1000,1000),RLSE),
//             DCB=(LRECL=200,BLKSIZE=0,RECFM=FB)
//SASLIST   DD SYSOUT=X
//SYSIN     DD *
OPTIONS NOCENTER;

 DATA CCPARTNER;
 KEEP CUSTNO PARTNER_INDC CODE DESC ;
 INFILE CCFILE;
    INPUT @01   CUSTNO            $11.      /* CUST1 - CIS#          */
          @15   INDORG             $1.      /* CUST1 - CUST TYPE     */
          @20   CODE               $3.      /* CUST1 - RLEN CODE     */
          @25   DESC              $15. ;    /* CUST1 - RLEN DESC     */
    IF CODE = '050';
    PARTNER_INDC = 'Y';
 RUN;
 PROC SORT  DATA=CCPARTNER NODUPKEY; BY CUSTNO ;RUN;
 PROC PRINT DATA=CCPARTNER (OBS=10);TITLE 'CCPARTNER  ';RUN;

 DATA RLEN017 RLEN020;
 KEEP ACCTNO CUSTNO BASICGRPCODE;
    SET CISFILE.CUSTDLY;
    IF ACCTCODE IN ('LN','DP');
    IF 1000000000 < ACCTNO < 1999999999
    OR 2000000000 < ACCTNO < 2999999999
    OR 3000000000 < ACCTNO < 3999999999
    OR 4000000000 < ACCTNO < 4999999999
    OR 5000000000 < ACCTNO < 5999999999     /*194587*/
    OR 6000000000 < ACCTNO < 6999999999
    OR 7000000000 < ACCTNO < 7999999999
    OR 8000000000 < ACCTNO < 8999999999 ;
    IF ACCTNO EQ '  ' THEN DELETE;

    IF RLENCODE = 017 THEN DO;     /* GUARANTOR              */
       OUTPUT RLEN017;
    END;

    IF RLENCODE = 020 THEN DO;     /* BORROWER /HIRER        */
       OUTPUT RLEN020;
    END;
 RUN;

 DATA RLEN017;
     SET RLEN017;
     GTOR_INDC = 'Y';
 RUN;

 DATA RLEN020;
     SET RLEN020;
     BORROWER_INDC = 'Y';
 RUN;
 PROC SORT  DATA=RLEN017  ; BY ACCTNO  CUSTNO ;RUN;
 PROC SORT  DATA=RLEN020  ; BY ACCTNO  CUSTNO ;RUN;
 PROC PRINT DATA=RLEN017 (OBS=10);TITLE 'RLEN017    ';RUN;
 PROC PRINT DATA=RLEN020 (OBS=10);TITLE 'RLEN020    ';RUN;

 DATA MERGE_RLEN;
   MERGE RLEN017(IN=A) RLEN020(IN=B);
         BY ACCTNO  CUSTNO;
   IF GTOR_INDC     = ' ' THEN GTOR_INDC     = 'N';
   IF BORROWER_INDC = ' ' THEN BORROWER_INDC = 'N';
 RUN;
 PROC SORT  DATA=MERGE_RLEN NODUPKEY  ; BY CUSTNO ACCTNO  ;RUN;
 PROC PRINT DATA=MERGE_RLEN (OBS=10)  ;TITLE 'MERGE_RLEN ';RUN;

 DATA LNCUST;
   SET CISFILE.CUSTDLY;
   KEEP CUSTNO ACCTNO ACCTNOC PRISEC TAXID
   RLENCODE RELATIONDESC ACCTCODE BASICGRPCODE;

   IF ACCTCODE IN ('LN','DP');
   IF 1000000000 < ACCTNO < 1999999999
   OR 2000000000 < ACCTNO < 2999999999
   OR 3000000000 < ACCTNO < 3999999999
   OR 4000000000 < ACCTNO < 4999999999
   OR 5000000000 < ACCTNO < 5999999999      /*194587*/
   OR 6000000000 < ACCTNO < 6999999999
   OR 7000000000 < ACCTNO < 7999999999
   OR 8000000000 < ACCTNO < 8999999999 ;
   IF ACCTNO EQ '  ' THEN DELETE;

 RUN;
 PROC SORT DATA=LNCUST; BY CUSTNO ACCTNO;RUN;
 PROC PRINT DATA=LNCUST(OBS=10); TITLE'LNCUST';RUN;

 DATA MERGE_GTOR;
   MERGE LNCUST(IN=C) MERGE_RLEN(IN=D);
         BY CUSTNO ACCTNO;
   IF C;
 RUN;
 PROC SORT  DATA=MERGE_GTOR NODUPKEY  ; BY CUSTNO ACCTNO ;RUN;
 PROC PRINT DATA=MERGE_GTOR   (OBS=10);TITLE 'MERGE_GTOR ';RUN;

 DATA MERGE_PARTNER;
   MERGE MERGE_GTOR(IN=F) CCPARTNER(IN=G);
         BY CUSTNO;
   IF F;
 RUN;
 PROC SORT  DATA=MERGE_PARTNER ; BY CUSTNO ACCTNO ;RUN;
 PROC PRINT DATA=MERGE_PARTNER(OBS=10);TITLE 'MERGE_PARTNER ';RUN;

 DATA IDS;
    SET IDFILE.CUSTIDS;
        KEEP CUSTNO ALIASKEY ALIAS;
        IF ALIASKEY IN ('IC','BC','PP','ML','PL',
                        'BR','CI','PC','SA','GB','LP');
 RUN;
 PROC SORT DATA=IDS;BY CUSTNO;RUN;
 PROC PRINT DATA=IDS (OBS=10);TITLE 'CUST IDS FILE';RUN;

 PROC SQL;
 CREATE TABLE LNDETL AS
   SELECT MERGE_PARTNER.CUSTNO
         ,MERGE_PARTNER.ACCTNOC
         ,MERGE_PARTNER.TAXID
         ,MERGE_PARTNER.PRISEC
         ,MERGE_PARTNER.BORROWER_INDC
         ,MERGE_PARTNER.GTOR_INDC
         ,MERGE_PARTNER.PARTNER_INDC
         ,MERGE_PARTNER.BASICGRPCODE
         ,IDS.ALIASKEY
         ,IDS.ALIAS
     FROM  MERGE_PARTNER,IDS
     WHERE MERGE_PARTNER.CUSTNO = IDS.CUSTNO;
 QUIT;

 PROC SORT DATA=LNDETL;BY ACCTNOC CUSTNO;RUN;
 PROC PRINT DATA=LNDETL(OBS=10);TITLE'LNDETL';RUN;

 DATA NEWIC;
      SET LNDETL;
      CUSTID=ALIASKEY||ALIAS;
      TAXID ='';

 RUN;

 DATA OLDIC;
      SET LNDETL;
      CUSTID='OC '||TAXID;
      ALIASKEY='';
      ALIAS='';
      IF TAXID EQ '' THEN DELETE;
      IF TAXID EQ '000000000' THEN DELETE;
 RUN;
 PROC SORT DATA=OLDIC NODUPKEY;BY CUSTNO ACCTNOC;RUN;

 DATA CUSTIDS;
      SET NEWIC OLDIC;
 RUN;
 PROC SORT DATA=CUSTIDS;BY CUSTNO ACCTNOC CUSTID;RUN;

 DATA OUT;
   SET CUSTIDS;
   FILE OUTFILE;
   IF PRISEC = 901 THEN PRIMSEC = 'P';
   IF PRISEC = 902 THEN PRIMSEC = 'S';
   IF GTOR_INDC     = ' ' THEN GTOR_INDC     = 'N';
   IF BORROWER_INDC = ' ' THEN BORROWER_INDC = 'N';
   IF PARTNER_INDC  = ' ' THEN PARTNER_INDC  = 'N';
   PUT @001 ACCTNOC            $11.
       @012 CUSTNO             $11.
       @023 CUSTID             $30.
       @054 PRIMSEC            $1.
       @056 BORROWER_INDC      $1.     /* HIRER      CA 020*/
       @058 GTOR_INDC          $1.     /* GUARANTOR  CA 017*/
       @060 PARTNER_INDC       $1.     /* PARTNER    CC 050*/
       @064 BASICGRPCODE       $5. ;
 RUN;
